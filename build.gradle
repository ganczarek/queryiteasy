repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'findbugs'

sourceSets {
    acceptanceTest {
        java {
            srcDir 'src/acceptanceTest/java'
        }
        resources {
            srcDir 'src/acceptanceTest/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.compileClasspath
        runtimeClasspath += compileClasspath
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

task acceptanceTest(type: Test) {
    description = 'Runs the acceptance tests'
    group = 'verification'
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath += sourceSets.acceptanceTest.runtimeClasspath
    reports.junitXml.destination = 'build/acceptance-test-results'
    reports.html.destination = 'build/reports/acceptanceTest'
    dependsOn(test)
}

check {
    dependsOn(acceptanceTest)
}


findbugs {
    toolVersion = "3.0.1"
    sourceSets = [sourceSets.main]
    ignoreFailures = false
    reportsDir = file("$project.buildDir/findbugsReports")
    effort = "max"
    reportLevel = "high"
}


dependencies {
    testCompile 'org.hamcrest:hamcrest-all:1.3', 'junit:junit:4.12', 'org.mockito:mockito-all:1.10.19'
    acceptanceTestCompile 'org.hsqldb:hsqldb:2.3.3'
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
    module {
        scopes.TEST.plus += [configurations.acceptanceTestCompile]
    }
}